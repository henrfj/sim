% This is the full code for the entire course! Have fun you guys :D
% Remember to reset odometry when reaching a new Obstacle
% FINAL VERSION MONDAY 17.11.2021 19:00


goto "mission"

label "mission"

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 1 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "boxdist" %odox is facing infront of the robot and y to the left side
laser "scanpush cmd='zoneobst'"
ystart=$odoy
followline "br" @v 0.12 : ($drivendist>1)
followline "br" @v 0.12 : ($l4<0.6)
followline "bm" @v 0.08 : ($l4<0.2)
yend=$odoy
y = ystart-yend
eval (y+0.2+0.26) %0.26 center of gravity + 0.2 the distance from box %eval sqrt(x*x+y*y)
turn 180 @v 0.15
followline "bl" @v 0.15 : ($crossingblackline==1 & $drivendist>0.7)
turn 180

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 2 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "boxgate"
laser "scanpush cmd='zoneobst'"
ignoreobstacles
followline "bl" @v 0.2 : ($l4<0.2)
ignoreobstacles
drive @v0.1 : ($crossingblackline==1)
ignoreobstacles
fwd 0.16 @v0.1
fwd -1 @v0.2
turn -90 @v0.1
drive @v0.2 : ($blacklinefound==1)
ignoreobstacles
fwd 0.20
ignoreobstacles
turn -360 : ($crossingblackline==1)
turn 90 @v 0.2
followline "bm" @v 0.22 : ($crossingblackline==1)
fwd 0.25
turn 90 @v 0.2
ignoreobstacles
followline "bm" @v 0.22 : ($crossingblackline==1)
ignoreobstacles
fwd 0.225
ignoreobstacles
followline "bm" @v 0.22 : ($crossingblackline==1)


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 3 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "loosegate"
laser "scanpush cmd='zoneobst'"
followline "bm" @v 0.1 : ($l0 < 0.6 & $l0 > 0) %found left side of the gate
temp = $l0
drive @v 0.1 : ($l0 >(temp+0.04)) %0.04 to make it more robust
followline "bm" @v 0.1 : ($drivendist > 0.1) %move forward not to itnerfere with left gate
followline "bm" @v 0.1 : ($l0 < 0.6 & $l0 > 0) %found right side of the gate
temp = $l0
drive @v 0.1 : ($l0 >temp+0.04)
fwd -0.02
turn 90 @v 0.1

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 4 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Hug the wall
label "wallhug"
laser "scanpush cmd='zoneobst'"
log "irdistright" "irdistfrontmiddle" "$l0" "$l4"
fwd 0.3 @v 0.2
drive @v 0.2 : ($irdistfrontmiddle<0.25)
turn 90 @v 0.1
walldist = $irdistright
eval walldist
% finding the first gate
followwall "r" 0.35 @v 0.1: ($irdistright>0.87)
fwd 0.42 %0.26+0.15+small gap(5cm)
turn -90 @v 0.1

% Driving through the first gate
drive @v 0.1 : ($l0 < 0.6 & $l0 > 0) %found left side of the gate
temp = $l0
drive @v 0.1 : ($l0 >(temp+0.4)) %0.4 to make it more robust
fwd 0.66 % 0.36(robot L) +0.35(distance from wall) -0.05(wall difference)
turn -90 @v 0.1
fwd 0.16 % move forward to detect the wall

% Finding the second gate
walldist = $irdistright
followwall "r" walldist @v 0.065: ($irdistright>0.87)
fwd 0.42 %0.26+0.15+small gap(5cm)
turn -90 @v 0.1
followline "bm" :($drivendist>1)
turn -180
followline "bm" @v 0.2 : ($crossingblackline==1)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 5 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
label "whiteline"
laser "scanpush cmd='zoneobst'" 

% Old version
background=$line0 $line1 $line2 $line3 $line4 $line5 $line6 $line7 
eval background
ignoreobstacles
followline "bm" @v 0.2 : ($crossingblackline==1)
fwd 0.43
followline "wm" @v 0.05 : ($crossingblackline==1)
fwd 0.13 @v 0.1
turn -90 @v 0.1

% New version
%turn 3 @v 0.2
%drive @v 0.1 : ($l4>1.5 | $l4<0.5 | ($l3>1.5 & $l5>1.5))
%turn -2 @v 0.2
%drive @v 0.1 : (($l3>0.7 & $l5>0.7) | $l4<0.7)
%turn 2 @v 0.2
%drive @v 0.1 : (($l3>0.7 & $l5>0.7) | $l4<0.7)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% OBSTACLE 6 %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% followline
label "garage"
laser "scanpush cmd='zoneobst'"

% Find Garage
fwd 0.05 @v 0.2
followline "bm" @v 0.1 : ($l4<0.2)
turn -90

% Move up in front of the gate to the edge
drive @v 0.1 : ($irdistleft>0.87)
fwd 0.5 % clear the corner
turn 90 @v 0.1
fwd 0.3 % go along the north wall

% Follow the north wall to the end of the wall
walldist = $irdistleft
followwall "l" walldist @v 0.15: ($drivendist>0.5)
drive @v 0.1 : ($irdistleft>0.87)
%radius = (walldist+0.1+0.485+0.3)/2 %gaps+robotwidth+garage
% turnr radius 180 @v 0.1 %dont delete these 2 cmnds
fwd 0.5 % clear the corner
turn 90 @v 0.1
fwd 0.15 % go along the west wall

% Follow the west wall to the end of the wall
walldist = $irdistleft
followwall "l" walldist @v 0.15: ($drivendist>0.3)
drive @v 0.1 : ($irdistleft>0.87)
fwd 0.35 % clear the corner
turn 90 @v 0.1
fwd 0.15 % go into the south wall
ignoreobstacles
drive @v 0.1 : ($irdistfrontleft<0.16)
ignoreobstacles
fwd 0.18 @v 0.1 % stop exactly at the gate handle

% Now we are holding the gate
ignoreobstacles
turnr 0.78 70 @v 0.1 % 0.5 (being the width of the garage) + 0.05 (being distance from wall) + 0.14 (being half the width of the SMR) = 0.69

% Now the gate is open!
fwd -0.05
turn 110
ignoreobstacles
followline "bm" @v 0.1 :($drivendist>0.5)
ignoreobstacles
followline "bm" @v 0.1 : ($irdistfrontmiddle<0.2)
stop
label "stopprogram"
